plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.freefair.lombok' version '8.13.1'
    id 'org.openapi.generator' version "7.11.0"
    id 'org.springframework.cloud.contract' version "4.2.1"
}

dependencies {
    implementation project(':eureka-server')
    implementation project(':config-server')
    implementation project(':api-gateway')
    implementation project(':notifications-service')
    implementation project(':exchange-service')
}


allprojects {
    group = 'ru.strbnm'
    version = '1.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.openapi.generator'
    apply plugin: 'org.springframework.cloud.contract'

    configurations.configureEach {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
    tasks.named('test') {
        useJUnitPlatform()
    }

    ext {
        set('springCloudVersion', "2024.0.1")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

// Отключаем задачи Java в корневом проекте
tasks.withType(JavaCompile).configureEach {
    onlyIf { false }
}

tasks.withType(Jar).configureEach {
    onlyIf { false }
}

afterEvaluate {
    tasks.matching { it.name.startsWith("contract") || it.name.contains("Contract") }.configureEach {
        enabled = false
    }
}