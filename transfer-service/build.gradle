import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'  // Для регистрации в Eureka и обнаружения других сервисов
    implementation 'org.springframework.cloud:spring-cloud-starter-config'  // Для взаимодействия с Config Server
    implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'  // Для работы Spring Cloud Bus c RabbitMQ
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework:spring-jdbc:6.2.3'
    implementation 'com.zaxxer:HikariCP:6.2.1'
    implementation 'org.liquibase:liquibase-core:4.31.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.postgresql:r2dbc-postgresql:1.0.7.RELEASE'
    testRuntimeOnly 'com.h2database:h2'
    implementation 'org.postgresql:postgresql'
    testRuntimeOnly 'io.r2dbc:r2dbc-h2:1.0.0.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test:3.7.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.rest-assured:spring-web-test-client:5.5.2'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
}

springBoot {
    buildInfo()  // исполнение задачи по добавлению информации о сборке
}

bootJar {
    launchScript()
    // добавление атрибутов к манифесту
    manifest {
        attributes(
                'Implementation-Title': 'Сервис перевода денег между счетами (Transfer)',
                'Build-Timestamp': System.currentTimeMillis(),
                'Built-By': project.hasProperty('builtBy') ? project.property('builtBy') : 'default-builder'
        )
    }
    // имя и версия создаваемого архива
    archiveBaseName = 'transfer-service'
    archiveVersion = '1.0.0'
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/openapi/transfer-openapi.yml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.strbnm.transfer_service.domain")
    invokerPackage.set("ru.strbnm.transfer_service")
    apiPackage.set("ru.strbnm.transfer_service.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            requestMappingMode: "controller",
            interfaceOnly: "true",
            library: "spring-boot",
            reactive: "true",
            useSpringBoot3: "true",
            useJakartaEe: "true",
            useTags: "true",
            dateLibrary: "java8",
            openApiNullable: "false",
            serializableModel: "true",
            returnSuccessCode: "true",
            bigDecimalAsString: "true",
            additionalModelTypeAnnotations: "@com.fasterxml.jackson.annotation.JsonIgnoreProperties(ignoreUnknown=true)",
    ])
    typeMappings.set(Double: "java.math.BigDecimal")
}

sourceSets.main.java.srcDir "$projectDir/build/generated/src/main/java"

tasks.register('buildNotificationsClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$rootDir/openapi/notification-openapi.yml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.strbnm.transfer_service.client.notifications.domain")
    invokerPackage.set("ru.strbnm.transfer_service.client.notifications")
    apiPackage.set("ru.strbnm.transfer_service.client.notifications.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true",
    ])
    typeMappings.set(Double: "java.math.BigDecimal")
}

tasks.named('compileJava') {
    dependsOn(tasks.buildNotificationsClient)
}

tasks.register('buildBlockerClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$rootDir/openapi/blocker-openapi.yml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.strbnm.transfer_service.client.blocker.domain")
    invokerPackage.set("ru.strbnm.transfer_service.client.blocker")
    apiPackage.set("ru.strbnm.transfer_service.client.blocker.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true",
    ])
    typeMappings.set(Double: "java.math.BigDecimal")
}

tasks.named('compileJava') {
    dependsOn(tasks.buildBlockerClient)
}

tasks.register('buildAccountsClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$rootDir/openapi/accounts-openapi.yml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.strbnm.transfer_service.client.accounts.domain")
    invokerPackage.set("ru.strbnm.transfer_service.client.accounts")
    apiPackage.set("ru.strbnm.transfer_service.client.accounts.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true",
    ])
    typeMappings.set(Double: "java.math.BigDecimal")
}

tasks.named('compileJava') {
    dependsOn(tasks.buildAccountsClient)
}

tasks.register('buildExchangeClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$rootDir/openapi/exchange-openapi.yml")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.strbnm.transfer_service.client.exchange.domain")
    invokerPackage.set("ru.strbnm.transfer_service.client.exchange")
    apiPackage.set("ru.strbnm.transfer_service.client.exchange.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true",
    ])
    typeMappings.set(Double: "java.math.BigDecimal")
}

tasks.named('compileJava') {
    dependsOn(tasks.buildExchangeClient)
}

contracts {
    testMode = "WebTestClient"
    baseClassForTests = 'ru.strbnm.transfer_service.BaseContractTest'
    generatedTestJavaSourcesDir = project.file('src/contractTest/java/ru/strbnm/transfer_service/generatedContract')
}

apply plugin: 'maven-publish'

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifact verifierStubsJar
        }
    }
    repositories {
        mavenLocal()
    }
}

//Зависимости задач Gradle
tasks.named('compileJava') {
    dependsOn(tasks.openApiGenerate)
}
tasks.named('build') {
    dependsOn 'verifierStubsJar'
    finalizedBy 'publishToMavenLocal'
}

//afterEvaluate {
//    tasks.matching { it.name.startsWith("contract") || it.name.contains("Contract") }.configureEach {
//        enabled = false
//    }
//}